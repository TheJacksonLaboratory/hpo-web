buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://dl.bintray.com/ajay-kumar/plugins" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
        classpath "org.grails.plugins:views-gradle:1.1.6"
 //       classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.14.2"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

version "0.1"
group "hpo.angular.app"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.plugins.views-json"
//apply plugin:"asset-pipeline"
apply plugin:"org.grails.grails-gsp"
apply plugin:"com.moowork.node"
apply plugin:"project-report"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://dl.bintray.com/ajay-kumar/plugins" }

   flatDir {
     dir("libs")
   }
}

integrationTest {
  systemProperties System.properties
}

dependencies {
  profile "org.grails.profiles:web"
  //Use this once phenols on maven
  compile 'org.monarchinitiative.phenol:phenol-core:1.6.0'
  compile 'org.monarchinitiative.phenol:phenol-io:1.6.0'
  compile 'org.monarchinitiative.phenol:phenol-annotations:1.6.0'
  compile 'com.google.guava:guava:28.2-jre'

  // Raw jar file with its dependencies. Remove when switch to maven
  //compile name:'phenol-core-1.2.1'
  //compile name:'phenol-io-1.2.1'
  // Raw jar file for loinc2hpo
  compile name:'Loinc2HpoLib'

  compile "org.prefixcommons:curie-util:0.0.2"
  compile group: 'net.sourceforge.owlapi', name: 'owlapi-distribution', version: '4.2.6'
  compile group: 'org.jgrapht', name: 'jgrapht-core', version: '1.2.0'

  compile 'com.google.guava:guava:22.0'
  compile "org.springframework.boot:spring-boot-starter-logging"
  compile "org.springframework.boot:spring-boot-autoconfigure"
  compile "org.grails:grails-core"
  compile "org.springframework.boot:spring-boot-starter-actuator"
  compile "org.springframework.boot:spring-boot-starter-tomcat"

  compile "org.grails:grails-logging"

  compile "org.grails:grails-plugin-databinding"
  compile "org.grails:grails-plugin-datasource"
  compile "org.grails:grails-plugin-interceptors"
  compile "org.grails:grails-plugin-i18n"
  compile "org.grails:grails-plugin-rest"
  compile "org.grails:grails-plugin-services"
  compile "org.grails:grails-plugin-url-mappings"
  compile "org.grails:grails-web-boot"

  compile "org.grails.plugins:async"
  compile "org.grails.plugins:cache"
  compile "org.grails.plugins:events"
  compile 'org.grails.plugins:external-config:1.1.2'
  compile "org.grails.plugins:gsp"

  compile "org.grails.plugins:hibernate5"
  compile "org.hibernate:hibernate-core:5.1.5.Final"

  compile "org.grails.plugins:scaffolding"
  compile "org.grails.plugins:views-json"
  compile "org.grails.plugins:views-json-templates"

  compile 'builders.dsl:spreadsheet-builder-poi:1.0.5'
  compile 'builders.dsl:spreadsheet-builder-groovy:1.0.5'

  console "org.grails:grails-console"

  compile 'org.grails.plugins:swagger:1.0.1'

  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'

  runtime 'org.grails.plugins:grails-console:2.1.1'
  runtime "org.glassfish.web:el-impl:2.1.2-b03"
  runtime "com.h2database:h2"
  runtime "mysql:mysql-connector-java:5.1.40"
  runtime "org.apache.tomcat:tomcat-jdbc"
  runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.14.2"

  testCompile "org.grails:grails-gorm-testing-support"
  testCompile "org.grails:grails-datastore-rest-client"
  testCompile "org.grails:grails-web-testing-support"

  testCompile "org.gebish:geb-core:3.4"
  testCompile "org.gebish:geb-spock:3.4"
  testCompile "io.github.bonigarcia:webdrivermanager:4.1.0"
  testCompile "org.seleniumhq.selenium:selenium-api:3.14.0"
  testCompile "org.seleniumhq.selenium:selenium-support:3.14.0"
  testCompile "org.seleniumhq.selenium:selenium-chrome-driver:3.14.0"
  testCompile "org.seleniumhq.selenium:selenium-remote-driver:3.14.0"
}


node {
    version = '12.0.0'
    npmVersion = '6.4.1'
    download = true
    nodeModulesDir = file("src/main/client")
}

task buildClientDev(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'build'
    description = 'Compile client side assets for development'
    args = ['run', 'build']
}

task buildClientProd(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'build'
    description = 'Compile client side assets for production'
    args = ['run', 'buildProd']
}

task buildClientWatch(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'application'
    description = 'Builds and watches the client side assets for rebuilding'
    args = ['run', 'buildWatch']
}

task clientTest(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'verification'
    description = 'Executes client side unit tests'
    args = ['run', 'test']
}

task clientIntegrationTest(type: NpmTask, dependsOn: 'npmInstall') {
    group = 'verification'
    description = 'Executes client side integration tests'
    args = ['run', 'e2e']
}

war {
  archiveName = System.getProperty("warName")
}

war.dependsOn(buildClientProd)

test.dependsOn(clientTest)
//integrationTest.dependsOn(clientIntegrationTest)

/* Integration test standard out testing

integrationTest {
  testLogging {
    exceptionFormat = 'full'
    events 'failed', 'standardOut', 'standardError'
  }
}*/

clean {
    delete fileTree(dir: "src/main/webapp/app")
}

bootRun {

    jvmArgs(
        '-server',
        '-Xmx768M',
        '-XX:MaxPermSize=256m',
        '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005',
        '-Dspring.output.ansi.enabled=always',
        '-Dgrails.reload.enabled=true'
    )
    addResources = true

  systemProperties System.properties
}
